{
  description = "My nix-darwin flake generated by `nix flake init -t nix-darwin`";

  inputs = {
    nixpkgs.url = "github:NixOS/nixpkgs/nixpkgs-unstable";
    nix-darwin = {
      url = "github:nix-darwin/nix-darwin";
      inputs.nixpkgs.follows = "nixpkgs";
    };
    home-manager = {
      url = "github:nix-community/home-manager/master";
      inputs.nixpkgs.follows = "nixpkgs";
    };
    mac-app-util.url = "github:hraban/mac-app-util";
  };

  outputs = inputs@{ self, nixpkgs, ... }:
    let
      # host configurations
      # Add new hosts here with their username and platform
      hosts = {
        roswell = {
          username = "yama";
          platform = "aarch64-darwin";
        };
      };

      # Base system configuration for nix-darwin
      configuration = host@{ username, platform, ... }: { pkgs, ... }: {
        # List packages installed in system profile. To search by name, run:
        # $ nix-env -qaP | grep wget
        environment.systemPackages =
          [
            # pkgs.vim
          ];

        # Necessary for using flakes on this system.
        nix.settings.experimental-features = [
          "nix-command"
          "flakes"
        ];

        # Enable alternative shell support in nix-darwin.
        # programs.fish.enable = true;

        # Set Git commit hash for darwin-version.
        system.configurationRevision = self.rev or self.dirtyRev or null;

        # Used for backwards compatibility, please read the changelog before changing.
        # $ darwin-rebuild changelog
        system.stateVersion = 6;

        # The platform the configuration will be used on.
        nixpkgs.hostPlatform = platform;

        # Allow unfree packages
        nixpkgs.config.allowUnfree = true;

        # when execute `sudo darwin-rebuild switch --flake <flake.nix dir>/#roswell`,
        # the following error appears.
        # error: Determinate detected, aborting activation
        # Determinate uses its own daemon to manage the Nix installation that
        # conflicts with nix-darwin's native Nix management.
        # To turn off nix-darwin's management of the Nix installation, set:
        nix.enable = false;

      };

      # Generate Home Manager module configuration
      mkHomeManagerModule = host@{ username, platform, ... }:
        let
          system = platform;
          pkgs = nixpkgs.legacyPackages.${system};
          isDarwin = pkgs.stdenv.isDarwin;
          homeDirectory = if isDarwin then "/Users/${username}" else "/home/${username}";
        in
          {
            home-manager.useGlobalPkgs = true;
            home-manager.useUserPackages = true;
            home-manager.extraSpecialArgs = {
              inherit inputs username homeDirectory isDarwin;
            };
            home-manager.users.${username} = {
              home.username = username;
              home.homeDirectory = pkgs.lib.mkForce homeDirectory;
              imports = [ ../home-manager/home.nix ];
            };
            home-manager.sharedModules = [
              # mac-app-util is already imported in ../home-manager/home.nix
              # mac-app-util.homeManagerModules.default
            ];
          };

      # make Darwin system configuration
      mkDarwinSystem = hostname: host@{ username, platform, ...}:
        inputs.nix-darwin.lib.darwinSystem {
          specialArgs = {
            inherit username;
          };
          modules = [
            # Base system configuration
            (configuration host)

            # Home Manager integration
            inputs.home-manager.darwinModules.home-manager

            # System-level feature configurations
            ./fonts.nix
            ./fish.nix
            ./finder.nix

            # homebrew
            ./homebrew.nix

            # User environment configuration
            (mkHomeManagerModule host)
          ];
        };
    in
      {
        # sudo darwin-rebuild switch --flake .#roswell
        darwinConfigurations = builtins.mapAttrs mkDarwinSystem hosts;
      };
}
